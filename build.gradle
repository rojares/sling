apply plugin: 'java-library'
apply plugin: 'maven-publish'

group = 'rojares.sling'
version = 'head'

sourceSets {
    main {
        java {
            srcDirs = ['src/java']
        }
    }

    test {
        java {
            srcDirs = ['test/java']
        }
    }
}

// enables jUnit 5 platform
test {
    useJUnitPlatform()
    // set log level for tests
    systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'trace'
}

repositories {
    maven {
        url "http://kiev.tv7.fi:8081/repository/maven-public/"
    }
}

dependencies {
    // david server
    testImplementation 'rojares.david:david:head'
    // ascii tables to support test output
    implementation 'com.github.freva:ascii-table:1.1.0'
    // slf4j api
    implementation 'org.slf4j:slf4j-api:1.7.30'
    // slf4j simple binding
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    // some collection support
    implementation 'org.apache.commons:commons-collections4:4.4'
    // jUnit 5
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
}

publishing {
	publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
			credentials {
                username "$nexus_username"
                password "$nexus_password"
            }
            url = "http://kiev.tv7.fi:8081/repository/exodus"
            allowInsecureProtocol = true
        }
    }
}

javadoc {
    dependsOn 'build'
    source = sourceSets.main.allJava
    title = "project: $project.name / version: $version"
}

task javadocPublish (type: Copy) {
    dependsOn javadoc
    from 'build/docs/javadoc'
    into "${javadoc_repository_path}/${project.name}"
}
